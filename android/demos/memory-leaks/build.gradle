/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath deps.build.gradlePlugins.android
        classpath deps.build.gradlePlugins.errorprone
    }
}

apply plugin: 'com.android.application'
apply plugin: 'net.ltgt.errorprone'

android {
    compileSdkVersion deps.build.compileSdkVersion
    buildToolsVersion deps.build.buildToolsVersion

    defaultConfig {
        minSdkVersion deps.build.minSdkVersion
        targetSdkVersion deps.build.targetSdkVersion
        applicationId "com.uber.tutorial3"
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    annotationProcessor project(":tooling:autodispose-error-prone-checker")
    annotationProcessor deps.apt.nullAway
    annotationProcessor deps.apt.daggerCompiler
    annotationProcessor project(":libraries:rib-compiler-app")
    annotationProcessor project(":libraries:rib-compiler-test")
    compile project(":libraries:rib-android")
    compile deps.external.dagger
    compile deps.external.rxbinding
    compile deps.external.leakcanaryDebug
    compile 'com.android.support:percent:25.0.1'
    provided deps.apt.javax
    testCompile project(":libraries:rib-test-utils")
    errorprone deps.build.errorProne
}

tasks.withType(JavaCompile) {
    // remove the if condition if you want to run NullAway on test code
    if (!name.toLowerCase().contains("test")) {
        options.compilerArgs += [
            "-Xep:NullAway:WARN",
            "-XepOpt:NullAway:AnnotatedPackages=com.uber",
            "-XepOpt:NullAway:KnownInitializers=com.uber.rib.core.Initializer",
            "-XepOpt:NullAway:ExcludedFieldAnnotations=javax.inject.Inject",
        ]
    }
    // Disable error prone checks I don't want.
    options.compilerArgs += [
            "-Xep:ReferenceEquality:OFF",
            "-Xep:ShortCircuitBoolean:OFF",
            "-Xep:MissingCasesInEnumSwitch:OFF",
            "-Xep:CheckReturnValue:OFF",
            "-Xep:InvalidPatternSyntax:OFF",
            "-Xep:OperatorPrecedence:OFF",
            "-Xep:DefaultCharset:OFF"]
    // Dagger doesn't pass the NullAway checks. So we need to set NullAway to null
    // use the following flag to disable nullaway on generated code.
    options.compilerArgs += [
            "-XepDisableWarningsInGeneratedCode"
    ]
}
